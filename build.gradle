allprojects {
    group = 'io.pipeline'
    version = '1.0.0-SNAPSHOT'

    repositories {
        // Maven Local first for fast local development
        mavenLocal()

        // GitHub Packages for published artifacts
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/io-pipeline/pipeline")
            credentials {
                // CI/CD uses GITHUB_ACTOR, local uses GH_USER
                username = System.getenv("GITHUB_ACTOR") ?: System.getenv("GH_USER")
                // CI/CD uses GITHUB_TOKEN, local uses GH_PAT
                password = System.getenv("GITHUB_TOKEN") ?: System.getenv("GH_PAT")
            }
        }

        mavenCentral()

        // Apache Snapshot repository for Tika 4.0.0-SNAPSHOT
        maven {
            url "https://repository.apache.org/content/repositories/snapshots/"
            content {
                includeGroup "org.apache.tika"
            }
            mavenContent {
                snapshotsOnly()
            }
        }
    }
}

// Configure publishing for subprojects
subprojects {
    pluginManager.withPlugin('maven-publish') {
        publishing {
            repositories {
                // Publish to Gitea Maven registry (primary)
                maven {
                    name = "Gitea"
                    url = uri("https://git.rokkon.com/api/packages/io-pipeline/maven")
                    credentials {
                        username = project.findProperty("gitea.user") ?: System.getenv("GITEA_USER") ?: "krickert"
                        password = project.findProperty("gitea.token") ?: System.getenv("GITEA_TOKEN")
                    }
                }

                // Publish to GitHub Packages (mirror)
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/io-pipeline/grpc")
                    credentials {
                        username = System.getenv("GITHUB_ACTOR") ?: System.getenv("GH_USER")
                        password = System.getenv("GITHUB_TOKEN") ?: System.getenv("GH_PAT")
                    }
                }
            }
        }
    }
}